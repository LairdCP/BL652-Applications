//Example :: BleTxPowerGet.sb

dim rc, hc, power, addr$

#define BLE_EVBLEMSGID_CONNECT              0    //msgCtx = connection handle
#define BLE_EVBLEMSGID_DISCONNECT           1    //msgCtx = connection handle

#define BLE_TXPOWERTYPE_SCANNING            0
#define BLE_TXPOWERTYPE_ADVERTISING         1
#define BLE_TXPOWERTYPE_PAIRING             2
#define BLE_TXPOWERTYPE_DEFAULT_CONNECTION  3
#define BLE_TXPOWERTYPE_ACTIVE_CONNECTION   4

sub AssertRC(rc, line)
    if rc!=0 then
        print "Failed with ";integer.h' rc;" on line ";line;"\n"
    endif
endsub

sub PrintChanelMap(nCtx)
    rc = BleTxPowerGet(BLE_TXPOWERTYPE_SCANNING, 0, power)
    AssertRC(rc, 22)
    print " - Scanning: ";power;" dBm"
    rc = BleTxPowerGet(BLE_TXPOWERTYPE_PAIRING, 0, power)
    AssertRC(rc, 25)
    print ", Pairing: ";power;" dBm"
    rc = BleTxPowerGet(BLE_TXPOWERTYPE_ACTIVE_CONNECTION, nCtx, power)
    AssertRC(rc, 28)
    print ", Active connection (";nCtx;"): ";power;" dBm\n"
endsub

//------------------------------------------------------------------------------
// This handler is called when BLE advertising times out
//------------------------------------------------------------------------------
function HndlrBlrAdvTimOut()
    print "Advertising timed out\n"
endfunc 1

//------------------------------------------------------------------------------
// This handler is called when there is a significant BLE event
//------------------------------------------------------------------------------
function HndlrBleMsg(byval nMsgId as integer, byval nCtx as integer)
    if (nMsgId == BLE_EVBLEMSGID_CONNECT) then
        hc = nCtx
        print "Connected"
        PrintChanelMap(nCtx)
        TimerStart(0, 3000, 0)
        rc = BleTxPowerset(0)
        AssertRC(rc, 49)
        print "Set Tx power to 0 dBm\n"
        rc = BleTxPwrWhilePairing(-8)
        AssertRC(rc, 52)
        print "Set Tx power whilst pairing to -8 dBm\n"
    elseif (nMsgId == BLE_EVBLEMSGID_DISCONNECT) then
        print "Disconnected\n"
        TimerCancel(0)
    endif
endfunc 1

//------------------------------------------------------------------------------
// This handler is called when timer 0 elapses
//------------------------------------------------------------------------------
function HndlrTimer0()
    print "After 3 seconds"
    PrintChanelMap(hc)
endfunc 1

//******************************************************************************
// Equivalent to main() in C
//******************************************************************************

//Start advertising over BLE
if BleAdvertStart(0, addr$, 250, 60000, 0) == 0 then
    addr$ = sysinfo$(4)
    print "Adverts Started - connect to device named: ";BleGetDeviceName$();", address: ";strhexize$(addr$);"\n"
else
    AssertRC(rc, 77)
    print "Adverts failed to start\n"
endif

//Register event handlers
OnEvent  EVBLEMSG           call HndlrBleMsg
OnEvent  EVBLE_ADV_TIMEOUT  call HndlrBlrAdvTimOut
OnEvent  EVTMR0             call HndlrTimer0

//Wait for an event
WAITEVENT
