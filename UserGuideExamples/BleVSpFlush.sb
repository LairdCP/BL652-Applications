//Example :: BleVSpFlush.sb

DIM conHndl      
              

//==============================================================================
// Open VSp and start advertising
//==============================================================================
SUB OnStartup()
    DIM rc, hndl, tx$, scRpt$, addr$, adRpt$ : adRpt$="" : addr$=""
    rc=BleVSpOpen(128,128,0,hndl)
    rc=BleScanRptInit(scRpt$)
    rc=BleAdvRptAddUuid128(scRpt$,hndl)
    rc=BleAdvRptsCommit(adRpt$,scRpt$)
    rc=BleAdvertStart(0,addr$,20,300000,0)
    rc=GpioBindEvent(1,11,1)     //Channel 1, bind to low transition on GPIO pin 11
    PRINT "\nDevice name is "; BleGetDeviceName$()

    tx$="\nSend me some text, I won't get it. \nTo exit the app press Button 1\n"
    rc = BleVSpWrite(tx$)
ENDSUB

//==============================================================================
// Close connections so that we can run another app without problems
//==============================================================================
SUB CloseConnections()
    DIM rc
    rc=BleDisconnect(conHndl)
    rc=BleAdvertStop()
    BleVspClose()
    BleVspFlush(3)      //Flush both buffers
ENDSUB

//==============================================================================
//  VSP Rx buffer event handler
//==============================================================================
FUNCTION HandlerVSpRx() AS INTEGER    
    BleVspFlush(1)
    PRINT "\nRx buffer flushed"
ENDFUNC 1

//==============================================================================
//handler to service button 1 pressed
//==============================================================================
FUNCTION HndlrBtn1Pr() AS INTEGER
    //stop waiting for events and exit app
ENDFUNC 0


//==============================================================================
// BLE event handler
//==============================================================================
FUNCTION HndlrBleMsg(BYVAL nMsgId, BYVAL nCtx) 
    conHndl=nCtx
    IF nMsgID==1 THEN
        PRINT "\nDisconnected from client"
        EXITFUNC 0
    ENDIF
ENDFUNC 1


ONEVENT  EVVSPRX      CALL HandlerVSpRx
ONEVENT  EVBLEMSG     CALL HndlrBleMsg
ONEVENT  EVGPIOCHAN1  CALL HndlrBtn1Pr


OnStartup()          //Calls first subroutine declared above

WAITEVENT

CloseConnections()  //Calls second subroutine declared above
PRINT "\nExiting..."