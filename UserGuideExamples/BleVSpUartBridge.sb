//Example :: BleVSpUartBridge.sb

DIM conHndl

//==============================================================================
// Open VSp and start advertising
//==============================================================================
SUB OnStartup()
    DIM rc, hndl, tx$, scRpt$, addr$, adRpt$ 
    
    rc=BleVSpOpen(128,128,0,hndl)
    rc=BleScanRptInit(scRpt$) 
    rc=BleAdvRptAddUuid128(scRpt$,hndl)
    rc=BleAdvRptsCommit(adRpt$,scRpt$)
    rc=BleAdvertStart(0,addr$,20,300000,0)
    rc=GpioBindEvent(1,16,1)     //Channel 1, bind to low transition on GPIO pin 16
    PRINT "\nDevice name is "; BleGetDeviceName$();"\n"
    
    tx$="\nSend me some text. \nPress button 0 to exit\n"
    rc = BleVSpWrite(tx$)
ENDSUB


//==============================================================================
// Close connections so that we can run another app without problems
//==============================================================================
SUB CloseConnections()
    DIM rc
    rc=BleDisconnect(conHndl)
    rc=BleAdvertStop()
    BleVspClose()
ENDSUB


//==============================================================================
// BLE event handler - connection handle is obtained here
//==============================================================================
FUNCTION HndlrBleMsg(BYVAL nMsgId, BYVAL nCtx) 
    conHndl=nCtx
    IF nMsgID==1 THEN
        PRINT "\nDisconnected from client"
        EXITFUNC 0
    ENDIF
ENDFUNC 1


//==============================================================================
//handler to service button 0 pressed
//==============================================================================
FUNCTION HndlrBtn0Pr() AS INTEGER
    //just exit and stop waiting for events
ENDFUNC 0


//==============================================================================
//handler to service an rx/tx event
//==============================================================================
FUNCTION HandlerBridge() AS INTEGER
  // transfer data between VSP and UART ring buffers
  BleVspUartBridge()
ENDFUNC 1
    
    

ONEVENT  EVVSPRX            CALL HandlerBridge
ONEVENT  EVUARTRX           CALL HandlerBridge
ONEVENT  EVVSPTXEMPTY       CALL HandlerBridge
ONEVENT  EVUARTTXEMPTY      CALL HandlerBridge
ONEVENT  EVBLEMSG          CALL HndlrBleMsg
ONEVENT  EVGPIOCHAN1       CALL HndlrBtn0Pr     


OnStartup()

WAITEVENT

CloseConnections()  //Calls second subroutine declared above
PRINT "\nExiting..."
